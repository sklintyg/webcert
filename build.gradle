ext {
    commonVersion = System.properties['commonVersion'] ?: '0-SNAPSHOT'
    infraVersion = System.properties['infraVersion'] ?: '0-SNAPSHOT'

    commonsIoVersion = "2.4"
    funktionstjansterGrpSchemasVersion = "1.0.3"
    insurancemedicinedecisionsupportSchemasVersion = "1.0.1"
    intygInsuranceprocessSchemasVersion = "1.0.4"
    intygTransportstyrelsenSchemasVersion = "1.0.4"
    ppSchemasVersion = "1.0.4"
    ppTermsSchemasVersion = "1.0.4"
    rivtaClinicalprocessSchemasVersion = "2.0"
    rivtaInsuranceprocessSchemasVersion = "3.1.0.4"
    rivtaItintegrationMonitoringSchemasVersion = "1.0.0.3"
    rivtaItintegrationSchemasVersion = "1.0.0.4"
    rivtaResidentMasterSchemasVersion = "1.1.3.5"

    activeMqVersion = "5.13.4"
    aspectJWeaverVersion = "1.7.1"
    awaitilityVersion = "1.4.0"
    camelVersion = "2.16.0"
    dbUnitVersion = "2.4.9"
    fitnesseVersion = "20150814"
    gebCoreVersion = "0.10.0"
    geronimoJmsVersion = "1.1.1"
    groovyVersion = "2.4.7"
    hamcrestVersion = "1.3"
    httpBuilderVersion = "0.7.1"
    jadiraUserTypeVersion = "5.0.0.GA"
    javaxMailVersion = "1.4"
    javaxWsRsApiVersion = "2.0.1"
    jaxrsVersion = "1.5.4"
    jmsApiVersion = "1.1-rev-1"
    jsonAssertVersion = "1.2.0"
    jsonSchemaValidatorVersion = "2.8.0"
    liquibaseVersion = "3.3.5"
    luceneAnalyzersCommonVersion = "4.10.2"
    openWsVersion = "1.4.2-1"
    opensamlVersion = "2.5.3"
    restassuredVersion = "2.8.0"
    seleniumServerVersion = "2.52.0"
    seleniumVersion = "2.52.0"
    servletApiVersion = "2.5"
    springDataVersion = "1.9.0.RELEASE"
    springSecurityVersion = "4.0.3.RELEASE"
    springVersion = "4.2.2.RELEASE"
    stAntlr4Version = "4.0.8"
    xmlToolingVersion = "1.3.2"
    xmlUnitVersion = "1.4"

    mockitoVersion = "1.9.0"
    springockitoVersion = "1.0.8"
    mysqlVersion = "5.1.39"
    h2Version = "1.3.176"
}

buildscript {
    repositories {
        mavenLocal()
        maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/releases/" }
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath 'se.inera.intyg:gradle-intyg-plugin:1.0.+'
        classpath 'se.inera.intyg:gradle-fitnesse-plugin:1.0.+'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.+'
        classpath 'org.ajoberstar:gradle-git:1.+'
        classpath 'org.akhikhl.gretty:gretty:+'
        classpath 'com.moowork.gradle:gradle-node-plugin:0.+'
        classpath 'com.moowork.gradle:gradle-grunt-plugin:0.+'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.+'
        classpath 'net.ltgt.gradle:gradle-errorprone-plugin:0.0.8'
    }
}

allprojects {
    group = 'se.inera.intyg.webcert'
    version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

    apply plugin: 'inera-intyg'
    apply plugin: 'maven'
    apply plugin: 'org.ajoberstar.grgit'
}

subprojects {
    repositories {
        mavenLocal()
        maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/releases/" }
        maven { url "http://repo.maven.apache.org/maven2" }
    }

    dependencies {
        testCompile 'junit:junit:4.+'
        testCompile "org.mockito:mockito-core:${mockitoVersion}"
    }

    apply plugin: 'java'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task createVersionPropertyFile(type: VersionPropertyFileTask)

    uploadArchives.repositories.mavenDeployer {
        repository(url: "https://build-inera.nordicmedtest.se/nexus/content/repositories/releases") {
            authentication(userName: System.properties['nexusUsername'], password: System.properties['nexusPassword'])
        }
    }

    [install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
        pom.dependencies.findAll {dep -> dep.groupId == 'se.inera.intyg.common'}.each {
            it.version = findResolvedVersion(project, 'se.inera.intyg.common')
        }
        pom.dependencies.findAll {dep -> dep.groupId == 'se.inera.intyg.infra'}.each {
            it.version = findResolvedVersion(project, 'se.inera.intyg.infra')
        }
    }
}

task tagRelease(type: TagReleaseTask)

task testReport(type: SharedTestReportTask)

apply from: file("codequality.gradle")
