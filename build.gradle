plugins {
    alias(libs.plugins.com.github.ben.manes.versions)
    alias(libs.plugins.org.cyclonedx.bom)
    alias(libs.plugins.org.sonarqube)
    id "org.gretty" version "4.1.10" apply false
}

ext {
    commonVersion = System.properties['commonVersion'] ?: '4.1.0-SNAPSHOT'
    infraVersion = System.properties['infraVersion'] ?: '4.1.0-SNAPSHOT'
    refDataVersion = '2.0.22'

    intygClinicalprocessSchemasVersion = "1.0.24.2"
    intygHealthcondRehabilitationSchemasVersion = "1.1.3.2"
    rivtaClinicalprocessSchemasVersion = "4.0.5.3"
    rivtaInsuranceprocessSchemasVersion = "3.1.0.5.2"
    schemasContractVersion = "2.1.8.2"
    srsSchemasVersion = "0.0.16.2"

    xsdgenVersion = "4.0.6"; // Look at replacing this to simplify
}

apply from: 'gradle/configureIntygBom.gradle'

allprojects {
    apply plugin: 'java'
    apply plugin: 'test-report-aggregation'

    group = 'se.inera.intyg.webcert'
    version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

    repositories {
        mavenLocal()
        maven { url "https://nexus.drift.inera.se/repository/it-public/" }
        maven { url 'https://build.shibboleth.net/nexus/content/repositories/releases/' }
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'jacoco'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(javaVersion)
        }
    }

    configurations {
        mockitoAgent
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    compileJava {
        options.encoding = "UTF-8"
        options.compilerArgs.add("-parameters")
    }

    dependencies {
        implementation platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        annotationProcessor platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        testImplementation platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        testAnnotationProcessor platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        mockitoAgent platform("se.inera.intyg.bom:platform:${intygBomVersion}")
        mockitoAgent("org.mockito:mockito-core") { transitive = false }
    }

    test {
        jvmArgs += [
                "-javaagent:${configurations.mockitoAgent.asPath}",
                "-Xshare:off"
        ]
    }

    testing {
        suites {
            test {
                useJUnitJupiter()
            }
        }
    }

    jacocoTestReport {
        dependsOn test

        reports {
            xml.required = true
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, exclude: [
                        "**/testability/**",
                        "**/integrationtest/**"
                ])
            }))
        }
    }

    tasks.matching { it.name == "cyclonedxDirectBom" }.configureEach { t ->
        t.includeConfigs = ['^runtimeClasspath$']
    }
}

reporting {
    subprojects.collect { subproject -> subproject.name }.each { subprojectName ->
        dependencies { testReportAggregation project(":${subprojectName}") }
    }
}

sonar {
    properties {
        property "sonar.projectKey", "intyg-webcert"
        property "sonar.projectName", "intyg-webcert"
    }
}
