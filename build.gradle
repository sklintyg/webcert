plugins {
  id 'org.ajoberstar.grgit' version '1.5.1'
  id 'org.sonarqube' version '2.1-rc3'
}

ext.nexusUsername = System.properties['nexusUsername']
ext.nexusPassword = System.properties['nexusPassword']

ext {
  springVersion = "4.2.2.RELEASE"
  springSecurityVersion = "4.0.3.RELEASE"
  camelVersion = "2.16.0"
  hamcrestVersion = "1.3"
  commonsIoVersion = "2.4"
  cxfVersion = "3.1.3"
  ppSchemasVersion = "1.0.2"
  ppTermsSchemasVersion = "1.0.2"
  healthcondSchemasVersion = "2.0.RC5.1"
  healthreportingSchemasVersion = "3.1.0.1"
}

allprojects  {
  apply plugin: 'maven'

  def baseVersion = '5.0.'
  def commonBaseVersion = '3.0.'

  group = 'se.inera.intyg.webcert'
  version = System.env.BUILD_NUMBER != null ? baseVersion + System.env.BUILD_NUMBER : '0-SNAPSHOT'
  rootProject.ext.commonVersion = (version == '0-SNAPSHOT') ? '0-SNAPSHOT' : commonBaseVersion + '+'
  rootProject.ext.typerVersion = (version == '0-SNAPSHOT') ? '0-SNAPSHOT' : commonBaseVersion + '+'
}

subprojects {
  apply plugin: 'java'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  repositories {
    mavenLocal()

    maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/releases/" }
    maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/nightly/" }
    maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/snapshots/" }
    maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/thirdparty/" }
    maven { url "http://repo.maven.apache.org/maven2" }
  }

  task createVersionPropertyFile(dependsOn: processResources) {
    def propertyFile = "${buildDir}/resources/main/version.properties"
    def projectVersion = rootProject.version
    def gitCommit = grgit.head().id
    def gitBranch = grgit.branch.getCurrent().getName()
    def buildNumber = System.env.BUILD_NUMBER

    inputs.property("project.version", projectVersion)
    inputs.property("gitCommit", gitCommit)
    inputs.property("gitBranch", gitBranch)
    inputs.property("buildNumber", buildNumber)
    outputs.file(propertyFile)

    doLast {
      ant.touch(file: propertyFile, mkdirs: "true")
      ant.propertyfile(file: propertyFile) {
        entry(key: 'project.version', default: projectVersion)
        entry(key: 'gitCommit', default: gitCommit)
        entry(key: 'gitBranch', default: gitBranch)
        entry(key: 'buildNumber', default: buildNumber)
        entry(key: 'buildTime', default: new Date())
      }
    }
  }

  jar.dependsOn(createVersionPropertyFile)

  configurations {
    buildtools
  }

  dependencies {
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:1.9.0'
    buildtools "se.inera.intyg.common:common-build-tools:${commonVersion}"
  }

  buildscript {
    repositories {
      mavenLocal()
      maven { url "https://build-inera.nordicmedtest.se/nexus/content/repositories/snapshots/" }
      maven { url "https://plugins.gradle.org/m2/" }
      jcenter()
    }
  }

  uploadArchives {
    repositories.mavenDeployer {
      pom.whenConfigured {pom ->
        pom.dependencies.findAll {dep -> dep.groupId == 'se.inera.intyg.common' }.each { it.version = rootProject.resolvedCommonVersion }
        pom.dependencies.findAll {dep -> dep.groupId == 'se.inera.intyg.intygstyper' }.each { it.version = rootProject.resolvedTyperVersion }
      }
      repository(url: "https://build-inera.nordicmedtest.se/nexus/content/repositories/releases") {
        authentication(userName: nexusUsername, password: nexusPassword)
      }
    }
  }

  install {
    repositories.mavenInstaller {
      pom.whenConfigured {pom ->
        pom.dependencies.findAll {dep -> dep.groupId == 'se.inera.intyg.common' }.each { it.version = rootProject.resolvedCommonVersion }
        pom.dependencies.findAll {dep -> dep.groupId == 'se.inera.intyg.intygstyper' }.each { it.version = rootProject.resolvedTyperVersion }
      }
    }
  }

  task unzipBuildTools(type: Copy) {
    description 'Extracts common-build-tools'
    into "$buildDir/build-tools/"
    from {
      configurations.buildtools.collect { zipTree(it) }
    }
  }

  if (project.hasProperty('codeQuality')) {
    apply plugin: 'checkstyle'

    checkstyle {
      config = resources.text.fromFile("${buildDir}/build-tools/checkstyle/checkstyle.xml")
      ignoreFailures = false
      showViolations = true
    }

    checkstyleMain.dependsOn unzipBuildTools
    checkstyleMain.source = "src/main/java"
    checkstyleTest.enabled = false
  }

  if (project.hasProperty('runSonar')) {
    apply plugin: 'jacoco'
    jacoco {
      toolVersion = "0.7.6.201602180812"
    }
    test {
      jacoco {
        destinationFile = file("$buildDir/jacoco/test.exec")
      }
    }
  }
}

project(':webcert-common') {
  afterEvaluate { project ->
    project.configurations.find{ it.name == 'compile' }.resolvedConfiguration.getFirstLevelModuleDependencies().find {
      if (it.moduleGroup == 'se.inera.intyg.common') {
        rootProject.ext.resolvedCommonVersion = it.moduleVersion
        return true
      }
      return false
    }
    project.configurations.find{ it.name == 'compile' }.resolvedConfiguration.getFirstLevelModuleDependencies().find {
      if (it.moduleGroup == 'se.inera.intyg.intygstyper') {
        rootProject.ext.resolvedTyperVersion = it.moduleVersion
        return true
      }
      return false
    }
  }
}

task tagRelease {
  description = 'Tags the current head with the projects version.'
  doLast {
    grgit.tag.add {
      name = version
      message = "Release of ${version}"
    }
    //grgit.push(tags: true)
  }
}

if (project.hasProperty('runSonar')) {
  apply plugin: "org.sonarqube"
  sonarqube {
    properties {
      property "sonar.jacoco.reportPath", "$buildDir/jacoco/test.exec"
      property "sonar.projectName", projectName
      property "sonar.projectKey", projectId

      // Address of SonarQube server
      property "sonar.host.url", "https://build-inera.nordicmedtest.se/sonar"

      ext.sonarPassword = System.properties['sonarPassword']

      //Exclusions
      property "sonar.test.exclusions", "src/test/**"

      property "sonar.exclusions", [
        "**/stub/**",
        "**/exception/**",
        "**/*Exception*.java",
        "**/*Fake*.java"
      ]

      //Skip these projects entirely
      project(":webcert-specifications") {
        sonarqube {
          skipProject = true
        }
      }
      project(":webcert-liquibase-runner") {
        sonarqube {
          skipProject = true
        }
      }
    }
  }
}
