plugins {
    id "se.inera.intyg.plugin.common" version "2.0.11" apply false
    id "org.gretty" version "2.3.1" apply false
    id "com.moowork.node" version "1.3.1" apply false
}

ext {
    commonVersion = System.properties['commonVersion'] ?: '0-SNAPSHOT'
    infraVersion = System.properties['infraVersion'] ?: '0-SNAPSHOT'
    refDataVersion = System.properties['refDataVersion'] ?: '1.0-SNAPSHOT'

    commonsIoVersion = "2.4"

    schemasContractVersion = "2.1.8"
    intygHealthcondRehabilitationSchemasVersion = "1.1.3"

    funktionstjansterGrpSchemasVersion = "1.0.6"
    intygClinicalprocessSchemasVersion = "1.0.22"
    ppSchemasVersion = "1.0.5"
    ppTermsSchemasVersion = "1.0.5"
    rivtaClinicalprocessSchemasVersion = "4.0.4"
    rivtaItintegrationMonitoringSchemasVersion = "1.0.0.5"

    activeMqVersion = "5.13.5"
    assertjVersion = "3.6.2"
    aspectJWeaverVersion = "1.7.1"
    autovalueVersion = "1.5"
    awaitilityVersion = "3.1.2"
    camelVersion = "2.16.0"
    dbUnitVersion = "2.4.9"
    geronimoJmsVersion = "1.1.1"
    hamcrestVersion = "1.3"
    httpComponentsVersion = "4.5"
    jadiraUserTypeVersion = "5.0.0.GA"
    javaxMailVersion = "1.4"
    swaggerJaxrsVersion = "1.5.21"
    jexcelVersion = "1.0.7"
    jsonAssertVersion = "1.5.0"
    jsonwebtokenVersion = "0.10.5"
    jwksVersion = "0.6.1"
    liquibaseVersion = "3.3.5"
    luceneAnalyzersCommonVersion = "5.5.2"
    openWsVersion = "1.4.2-1"
    opensamlVersion = "2.5.3"
    restassuredVersion = "2.9.0"
    servletApiVersion = "3.1.0"
    apacheJstlVersion = "9.3.6.v20151106"
    springDataVersion = "1.11.12.RELEASE"
    springDataRedisVersion = "1.8.12.RELEASE"
    springSecurityVersion = "4.2.5.RELEASE"
    springVersion = "4.3.17.RELEASE"
    springSessionVersion = "1.3.3.RELEASE"
    stAntlr4Version = "4.0.8"
    xmlToolingVersion = "1.3.2"
    xmlUnitVersion = "1.4"
    shedlockSpringVersion = "0.18.2"
    vavrVersion = "0.9.2"
    tomcatVersion = "7.0.57"

    mockitoVersion = "2.16.0"
    springockitoVersion = "1.0.8"
    mysqlVersion = "5.1.46"
    hikariCPVersion = "2.3.5"
    h2Version = "1.3.176"

    nodeJsVersion = "10.16.2"
    npmVersion = "6.10.3"
}

allprojects {
    apply plugin: 'se.inera.intyg.plugin.common'
    apply plugin: 'maven'
    apply plugin: 'idea'

    repositories {
        mavenLocal()
        maven { url "https://build-inera.nordicmedtest.se/nexus/repository/public/" }
        jcenter {
            content {
                // this repository contains everything BUT artifacts with group starting with "se.inera"
                excludeGroupByRegex "se\\.inera.*"
            }
        }
    }

    dependencies {
        testCompile "se.inera.intyg.refdata:refdata:${refDataVersion}"
        testCompile 'junit:junit:4.+'
        testCompile "org.mockito:mockito-core:${mockitoVersion}"
    }

    group = 'se.inera.intyg.webcert'
    version = System.properties['buildVersion'] ?: '0-SNAPSHOT'

    compileJava.options.encoding = 'UTF-8'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    task createVersionPropertyFile(type: VersionPropertyFileTask)

    uploadArchives.enabled = false

    uploadArchives.repositories.mavenDeployer {
        repository(url: "https://build-inera.nordicmedtest.se/nexus/repository/releases/") {
            authentication(userName: System.properties['nexusUsername'], password: System.properties['nexusPassword'])
        }
    }

    [install.repositories.mavenInstaller, uploadArchives.repositories.mavenDeployer]*.pom*.whenConfigured { pom ->
        pom.dependencies.findAll { dep -> dep.groupId == 'se.inera.intyg.common' }.each {
            it.version = se.inera.intyg.IntygPluginKt.findResolvedVersion(project, 'se.inera.intyg.common')
        }
        pom.dependencies.findAll { dep -> dep.groupId == 'se.inera.intyg.infra' }.each {
            it.version = se.inera.intyg.IntygPluginKt.findResolvedVersion(project, 'se.inera.intyg.infra')
        }
    }
}

task tagRelease(type: TagReleaseTask)
