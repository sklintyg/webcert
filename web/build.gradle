apply plugin: 'war'
apply plugin: 'org.gretty'
apply plugin: 'com.moowork.grunt'
apply plugin: 'idea'

def webappOutputDir = 'build/apps/'
def webappTestOutputDir = 'build/karma/'

idea {
    module {
        excludeDirs += file('node_modules')
        excludeDirs += file('src/main/webapp/bower_components')
    }
}

grunt {
    colors = System.properties['gruntColors'] ? System.properties['gruntColors'].toBoolean() : true
}

node {
    version = "${nodeJsVersion}"
    npmVersion = "${npmVersion}"
    download = true
    distBaseUrl = 'https://nodejs.org/dist'
    workDir = file("${rootProject.projectDir}/.gradle/nodejs")
    npmWorkDir = file("${rootProject.projectDir}/.gradle/npm")
}

task jsTests(type: NodeTask, dependsOn: npm_install) {
    inputs.dir("$projectDir/src/main/resources")
    inputs.dir("$projectDir/src/main/webapp")
    inputs.files(fileTree(projectDir).include('*.js'))
    inputs.files(fileTree(projectDir).include('*.json'))
    inputs.files(configurations.compile)
    outputs.dir(webappTestOutputDir)

    script = file('node_modules/grunt-cli/bin/grunt')
    args = ['test', "--outputDir=${webappTestOutputDir}", '--run-coverage=' + (project.findProperty('codeCoverage') != null ? 'true' : '')]
}

test.dependsOn(jsTests)

task jsInclude(type: NodeTask, dependsOn: npm_install) {
    inputs.dir("$projectDir/src/main/resources")
    inputs.dir("$projectDir/src/main/webapp")
    inputs.files(fileTree(projectDir).include('*.js'))
    inputs.files(fileTree(projectDir).include('*.json'))
    inputs.files(configurations.compile)
    outputs.dir(webappOutputDir)

    script = file('node_modules/grunt-cli/bin/grunt')
    args = ['default', "--outputDir=${webappOutputDir}"]
}

war.dependsOn(jsInclude)
war.dependsOn createVersionPropertyFile

task unzipBuildTools(type: Copy) {
    into "$buildDir/build-tools/"
    from {
        def intygJar = project.rootProject.buildscript.configurations.classpath.find { it.name.contains("se.inera.intyg.plugin.common") }
        zipTree(intygJar)
    }
}

task jsCodeQuality(type: NodeTask, dependsOn: [npm_install, unzipBuildTools]) {
    outputs.upToDateWhen { false }

    script = file('node_modules/grunt-cli/bin/grunt')
    args = ['lint']
}

checkstyleMain.dependsOn jsCodeQuality
jsCodeQuality.onlyIf { project.hasProperty('codeQuality') }

war {
    from("${buildDir}/apps")

    exclude '**/*.scss'
    exclude '**/*.spec.js'
    exclude '**/*.test.js'
}

test {
    exclude '**/*IT*'
}

task restAssuredTest(type: Test) {
    useJUnitPlatform()
    outputs.upToDateWhen { false }
    systemProperty "integration.tests.baseUrl", System.properties['baseUrl'] ?: "http://localhost:8020/"
    systemProperty "integration.tests.actuatorUrl", System.properties['actuatorUrl'] ?: "http://localhost:8120/"
    include '**/*IT*'
}

gretty {
    def instance = System.properties['instance'] ?: '0'
    def minified = System.properties['useMinifiedJavaScript'] ?: false
    def useHttpPort = 8020 + (instance as Integer)
    def useInternalHttpPort = 8120 + (instance as Integer)
    def applicationDir = "${rootProject.projectDir}/devops/dev"

    servletContainer = 'tomcat9'
    contextPath = '/'
    integrationTestTask = null
    scanInterval = 0
    debugPort = 8820 + (instance as Integer)
    portPropertiesFileName = "gretty_ports_${instance}.properties"
    fastReload = true
    inplaceMode = 'hard'
    debugSuspend = false
    serverConfigFile = "tomcat-gretty.xml"

    jvmArgs = ["-Dcatalina.base=${buildDir}/catalina.base",
               "-Dspring.profiles.active=dev,caching-enabled,wc-security-test,ia-stub",
               "-Dlogback.file=${applicationDir}/config/logback-dev.xml",
               "-Djava.awt.headless=true",
               "-Dfile.encoding=UTF-8",
               "-Dapplication.dir=${applicationDir}",
               "-Ddev.config.file=${applicationDir}/config/application-dev.properties",
               "-DuseMinifiedJavaScript=${minified}",
               "-Ddev.http.port=${useHttpPort}",
               "-Ddev.http.port.internal=${useInternalHttpPort}"]

    extraResourceBase webappOutputDir
}

configurations {
    // This is required to make the Schematron implementation pick up Saxon instead of Xalan.
    runtime {
        exclude group: 'xalan', module: 'xalan'
    }

    xsdgen
}

if (project.hasProperty("codeQuality")) {
    license {
        exclude "**/dss/xsd/**/*.java"
    }
}

ext.xsdDir = file("src/main/resources/schemas/dss")
ext.generatedJavaDir = file("${buildDir}/generatedSource/java")
ext.xsdBindings = file("src/main/resources/schemas/dss/bindings.xjb")

sourceSets.main.java {
    srcDir generatedJavaDir
}

idea {
    module {
        generatedSourceDirs.add(generatedJavaDir)
    }
}

task xsd2Java() {
    doLast {
        mkdir generatedJavaDir.path
        ant.taskdef(
                name: 'xjc',
                classname: 'com.sun.tools.xjc.XJCTask',
                classpath: configurations.xsdgen.asPath
        )
        ant.xjc(
                destdir: generatedJavaDir.path,
                binding: xsdBindings,
                fork: true) {
            schema(dir: xsdDir.path, includes: "**/*.xsd")
            produces(dir: generatedJavaDir.path, includes: "**/*")
            jvmarg(value: '-Djavax.xml.accessExternalDTD=file')
        }
    }
}

compileJava.dependsOn(xsd2Java)

dependencies {
    compile project(':fmb-integration')
    compile project(':pp-integration')
    compile project(':tak-integration')
    compile project(':kundportalen-integration')
    compile project(':notification-sender')
    compile project(':fk-stub')
    compile project(':notification-stub')
    compile project(':intygstjanst-stub')
    compile project(':mail-stub')

    compile "se.inera.intyg.infra:pu-integration:${infraVersion}"
    compile "se.inera.intyg.infra:srs-integration:${infraVersion}"
    compile "se.inera.intyg.infra:loggtjanst-stub:${infraVersion}"
    compile "se.inera.intyg.infra:security-filter:${infraVersion}"
    compile "se.inera.intyg.infra:security-siths:${infraVersion}"
    compile "se.inera.intyg.infra:dynamiclink:${infraVersion}"
    compile "se.inera.intyg.infra:grp-stub:${infraVersion}"
    compile "se.inera.intyg.infra:postnummerservice-integration:${infraVersion}"
    compile "se.inera.intyg.infra:xmldsig:${infraVersion}"
    compile "se.inera.intyg.infra:monitoring:${infraVersion}"
    compile "se.inera.intyg.infra:common-redis-cache-core:${infraVersion}"
    compile "se.inera.intyg.infra:sjukfall-engine:${infraVersion}"
    compile "se.inera.intyg.infra:ia-integration:${infraVersion}"
    compile "se.inera.intyg.infra:intyginfo:${infraVersion}"
    compile "se.inera.intyg.infra:integreradeenheter:${infraVersion}"
    compile "se.inera.intyg.infra:testcertificate:${infraVersion}"
    compile "se.inera.intyg.infra:certificate:${infraVersion}"
    compile "se.inera.intyg.infra:message:${infraVersion}"

    compile "se.inera.intyg.common:ts-bas:${commonVersion}"
    compile "se.inera.intyg.common:ts-diabetes:${commonVersion}"
    compile "se.inera.intyg.common:tstrk1062:${commonVersion}"
    compile "se.inera.intyg.common:luse:${commonVersion}"
    compile "se.inera.intyg.common:lisjp:${commonVersion}"
    compile "se.inera.intyg.common:luae_na:${commonVersion}"
    compile "se.inera.intyg.common:luae_fs:${commonVersion}"
    compile "se.inera.intyg.common:db:${commonVersion}"
    compile "se.inera.intyg.common:doi:${commonVersion}"
    compile "se.inera.intyg.common:af00213:${commonVersion}"
    compile "se.inera.intyg.common:af00251:${commonVersion}"
    compile "se.inera.intyg.common:ag114:${commonVersion}"
    compile "se.inera.intyg.common:ag7804:${commonVersion}"
    compile "se.inera.intyg.common:tstrk1009:${commonVersion}"

    compile "se.inera.intyg.infrastructure.directory.privatepractitioner.terms:intyg-infrastructure-directory-privatepractitioner-terms-schemas:${ppTermsSchemasVersion}"
    compile "se.inera.intyg.clinicalprocess.healthcond.certificate:intyg-clinicalprocess-healthcond-certificate-schemas:${intygClinicalprocessSchemasVersion}"
    compile "se.funktionstjanster.grp:funktionstjanster-grp-schemas:${funktionstjansterGrpSchemasVersion}"
    compile "se.riv.itintegration.monitoring:itintegration-monitoring-schemas:${rivtaItintegrationMonitoringSchemasVersion}"

    compile "se.inera.intyg.schemas:schemas-contract:${schemasContractVersion}"
    compile "se.inera.intyg.clinicalprocess.healthcond.rehabilitation:intyg-clinicalprocess-healthcond-rehabilitation-schemas:${intygHealthcondRehabilitationSchemasVersion}"

    compile "commons-codec:commons-codec"
    compile "commons-io:commons-io:${commonsIoVersion}"
    compile "org.jxls:jxls-jexcel:${jexcelVersion}"
    compile "org.springframework:spring-web"
    compile "org.springframework:spring-webmvc"
    compile "org.springframework.session:spring-session-data-redis"
    compile "org.springframework.security:spring-security-web"
    compile "org.springframework.security:spring-security-taglibs"
    compile "org.apache.lucene:lucene-analyzers-common:${luceneAnalyzersCommonVersion}"
    compile "org.opensaml:opensaml:${opensamlVersion}"
    compile "org.opensaml:openws:${openWsVersion}"
    compile "org.opensaml:xmltooling:${xmlToolingVersion}"
    compile("io.swagger:swagger-jaxrs:${swaggerJaxrsVersion}") {
        exclude(module: "jsr311-api")
    }

    compile "org.springframework.ws:spring-xml"

    compile "net.javacrumbs.shedlock:shedlock-spring:${shedlockSpringVersion}"
    compile "net.javacrumbs.shedlock:shedlock-provider-redis-spring:${shedlockSpringVersion}"

    compile "javax.servlet:javax.servlet-api"

    compile "io.jsonwebtoken:jjwt-impl:${jsonwebtokenVersion}"
    compile "io.jsonwebtoken:jjwt-jackson:${jsonwebtokenVersion}"

    compile "com.auth0:jwks-rsa:${jwksVersion}"
    compile "com.google.auto.value:auto-value:${autovalueVersion}"

    compile "org.apache.cxf:cxf-core:${cxfVersion}"
    compile "org.apache.cxf:cxf-rt-frontend-jaxws:${cxfVersion}"
    compile "org.apache.cxf:cxf-rt-frontend-jaxrs:${cxfVersion}"
    compile "org.apache.cxf:cxf-rt-transports-http:${cxfVersion}"

    compileOnly "org.apache.geronimo.specs:geronimo-jms_1.1_spec:${geronimoJmsVersion}"

    testCompile "se.inera.intyg.clinicalprocess.healthcond.certificate:intyg-clinicalprocess-healthcond-certificate-schemas:${intygClinicalprocessSchemasVersion}"
    testCompile "org.hamcrest:hamcrest"
    testCompile "org.hamcrest:hamcrest-library"
    testCompile "org.assertj:assertj-core"
    testCompile "org.skyscreamer:jsonassert"
    testCompile "org.springframework:spring-test"
    testCompile "org.xmlunit:xmlunit-legacy"
    testCompile "org.awaitility:awaitility"

    testCompile "io.rest-assured:rest-assured"
    testCompile "io.rest-assured:json-schema-validator"
    testCompile "com.sun.xml.bind:jaxb-osgi:2.3.2" // Can remove when rest-assured in BOM > version 4.0

    testCompile "org.antlr:ST4:${stAntlr4Version}"
    annotationProcessor "com.google.auto.value:auto-value:${autovalueVersion}"

    gretty "org.eclipse.jetty:apache-jstl:${apacheJstlVersion}"
    gretty "se.inera.intyg.refdata:refdata:${refDataVersion}"

    xsdgen "org.glassfish.jaxb:jaxb-xjc-jdk9:${jaxbXjcVersion}"
}