apply plugin: 'war'
apply plugin: 'org.akhikhl.gretty'
apply plugin: 'com.moowork.grunt'

def webappOutputDir = 'build/apps/'
def webappTestOutputDir = 'build/karma/'

grunt {
    colors = System.properties['gruntColors'] ? System.properties['gruntColors'].toBoolean() : true
}

node {
    version = '10.10.0'
    download = true
    distBaseUrl = 'https://build-inera.nordicmedtest.se/node/'
    workDir = projectDir
    nodeModulesDir = projectDir
}

task jsTests(type: NodeTask, dependsOn: npmInstall) {
    inputs.dir("$projectDir/src/main/resources")
    inputs.dir("$projectDir/src/main/webapp")
    inputs.files(fileTree(projectDir).include('*.js'))
    inputs.files(fileTree(projectDir).include('*.json'))
    inputs.files(configurations.compile)
    outputs.dir(webappTestOutputDir)

    script = file('node_modules/grunt-cli/bin/grunt')
    args = ['test', "--outputDir=${webappTestOutputDir}", '--run-coverage=' + (project.findProperty('codeCoverage') != null ? 'true' : '')]
}

test.dependsOn(jsTests)

task jsInclude(type: NodeTask, dependsOn: npmInstall) {
    inputs.dir("$projectDir/src/main/resources")
    inputs.dir("$projectDir/src/main/webapp")
    inputs.files(fileTree(projectDir).include('*.js'))
    inputs.files(fileTree(projectDir).include('*.json'))
    inputs.files(configurations.compile)
    outputs.dir(webappOutputDir)

    script = file('node_modules/grunt-cli/bin/grunt')
    args = ['default', "--outputDir=${webappOutputDir}"]
}

war.dependsOn(jsInclude)
war.dependsOn createVersionPropertyFile

task unzipBuildTools(type: Copy) {
    into "$buildDir/build-tools/"
    from {
        def intygJar = project.rootProject.buildscript.configurations.classpath.find { it.name.contains("se.inera.intyg.plugin.common") }
        zipTree(intygJar)
    }
}

task jsCodeQuality(type: NodeTask, dependsOn: [npmInstall, unzipBuildTools]) {
    outputs.upToDateWhen { false }

    script = file('node_modules/grunt-cli/bin/grunt')
    args = ['lint']
}

checkstyleMain.dependsOn jsCodeQuality
jsCodeQuality.onlyIf { project.hasProperty('codeQuality') }

war {
    from("${buildDir}/apps")

    exclude '**/*.scss'
    exclude '**/*.spec.js'
    exclude '**/*.test.js'
}

test {
    exclude '**/*IT*'
}

task restAssuredTest(type: Test) {
    outputs.upToDateWhen { false }
    systemProperty "integration.tests.baseUrl", System.properties['baseUrl'] ?: "http://localhost:9088/"
    include '**/*IT*'
}

task zipResources(type: ArchiveDirectoryTask) {
    sourcePath "../src/main/resources"
    include "*"
    include "*/*" //to include contents of a folder present inside from directory
    archiveName "resources.zip"
    destinationDir(file("../devops/openshift/test/env"))
    archiveOnlyIfDiff file("${destinationDir}/${archiveName}").exists()
}

build.dependsOn zipResources

gretty {
    contextPath = '/'
    contextConfigFile = file("src/main/webapp/WEB-INF/jetty-web.xml")
    integrationTestTask = null
    scanInterval = 0
    httpPort = 9088
    debugPort = 5007
    fastReload = true
    inplaceMode = 'hard'
    debugSuspend = false

    def minified = System.properties['useMinifiedJavaScript'] ?: false

    jvmArgs = ["-Dcatalina.base=${buildDir}/catalina.base",
               "-Dspring.profiles.active=dev,caching-enabled",
               "-Dresources.folder=${projectDir}/../src/main/resources",
               "-Dcredentials.file=${projectDir}/webcert-credentials.properties",
               "-Dconfig.file=${projectDir}/webcert-dev.properties",
               "-Dlogback.file=classpath:logback-ocp.xml",
               "-DuseMinifiedJavaScript=${minified}",
               "-Djetty.port=${httpPort}"]

    extraResourceBase webappOutputDir
}

configurations {
    // This is required to make the Schematron implementation pick up Saxon instead of Xalan.
    runtime {
        exclude group: 'xalan', module: 'xalan'
    }
}

dependencies {
    compile project(':fmb-integration')
    compile project(':pp-integration')
    compile project(':tak-integration')
    compile project(':notification-sender')
    compile project(':fk-stub')
    compile project(':notification-stub')
    compile project(':intygstjanst-stub')
    compile project(':mail-stub')

    compile "se.inera.intyg.infra:pu-integration:${infraVersion}"
    compile "se.inera.intyg.infra:srs-integration:${infraVersion}"
    compile "se.inera.intyg.infra:loggtjanst-stub:${infraVersion}"
    compile "se.inera.intyg.infra:security-filter:${infraVersion}"
    compile "se.inera.intyg.infra:security-siths:${infraVersion}"
    compile "se.inera.intyg.infra:dynamiclink:${infraVersion}"
    compile "se.inera.intyg.infra:grp-stub:${infraVersion}"
    compile "se.inera.intyg.infra:nias-stub:${infraVersion}"
    compile "se.inera.intyg.infra:postnummerservice-integration:${infraVersion}"
    compile "se.inera.intyg.infra:xmldsig:${infraVersion}"
    compile "se.inera.intyg.infra:monitoring:${infraVersion}"
    compile "se.inera.intyg.infra:common-redis-cache-core:${infraVersion}"
    compile "se.inera.intyg.infra:sjukfall-engine:${infraVersion}"

    compile "se.inera.intyg.common:ts-bas:${commonVersion}"
    compile "se.inera.intyg.common:ts-diabetes:${commonVersion}"
    compile "se.inera.intyg.common:luse:${commonVersion}"
    compile "se.inera.intyg.common:lisjp:${commonVersion}"
    compile "se.inera.intyg.common:luae_na:${commonVersion}"
    compile "se.inera.intyg.common:luae_fs:${commonVersion}"
    compile "se.inera.intyg.common:db:${commonVersion}"
    compile "se.inera.intyg.common:doi:${commonVersion}"
    compile "se.inera.intyg.common:af00213:${commonVersion}"
    compile "se.inera.intyg.common:af00251:${commonVersion}"
    compile "se.inera.intyg.common:ag114:${commonVersion}"
    compile "se.inera.intyg.common:ag7804:${commonVersion}"

    compile "se.inera.intyg.infrastructure.directory.privatepractitioner.terms:intyg-infrastructure-directory-privatepractitioner-terms-schemas:${ppTermsSchemasVersion}"
    compile "se.inera.intyg.clinicalprocess.healthcond.certificate:intyg-clinicalprocess-healthcond-certificate-schemas:${intygClinicalprocessSchemasVersion}"
    compile "se.funktionstjanster.grp:funktionstjanster-grp-schemas:${funktionstjansterGrpSchemasVersion}"
    compile "com.secmaker.netid.nias:netid-access-server-schemas:1.0.0"
    compile "se.riv.itintegration.monitoring:itintegration-monitoring-schemas:${rivtaItintegrationMonitoringSchemasVersion}"

    compile "se.inera.intyg.schemas:schemas-contract:${schemasContractVersion}"
    compile "se.inera.intyg.clinicalprocess.healthcond.rehabilitation:intyg-clinicalprocess-healthcond-rehabilitation-schemas:${schemasRehabilitationVersion}"

    compile "commons-codec:commons-codec:1.8"
    compile "commons-io:commons-io:${commonsIoVersion}"
    compile "org.jxls:jxls-jexcel:${jexcelVersion}"
    compile "org.springframework:spring-web:${springVersion}"
    compile "org.springframework:spring-webmvc:${springVersion}"
    compile "org.springframework.session:spring-session:${springSessionVersion}"
    compile "org.springframework.security:spring-security-web:${springSecurityVersion}"
    compile "org.springframework.security:spring-security-taglibs:${springSecurityVersion}"
    compile "org.apache.lucene:lucene-analyzers-common:${luceneAnalyzersCommonVersion}"
    compile "org.opensaml:opensaml:${opensamlVersion}"
    compile "org.opensaml:openws:${openWsVersion}"
    compile "org.opensaml:xmltooling:${xmlToolingVersion}"
    compile("io.swagger:swagger-jaxrs:${jaxrsVersion}") {
        exclude(module: "jsr311-api")
    }

    compile "net.javacrumbs.shedlock:shedlock-spring:${shedlockSpringVersion}"
    compile "net.javacrumbs.shedlock:shedlock-provider-redis-spring:${shedlockSpringVersion}"

    compile "javax.servlet:javax.servlet-api:${servletApiVersion}"

    compileOnly "org.apache.geronimo.specs:geronimo-jms_1.1_spec:${geronimoJmsVersion}"

    testCompile "se.inera.intyg.clinicalprocess.healthcond.certificate:intyg-clinicalprocess-healthcond-certificate-schemas:${intygClinicalprocessSchemasVersion}"
    testCompile "org.hamcrest:hamcrest-all:${hamcrestVersion}"
    testCompile "org.assertj:assertj-core:${assertjVersion}"
    testCompile "org.skyscreamer:jsonassert:${jsonAssertVersion}"
    testCompile "org.springframework:spring-test:${springVersion}"
    testCompile "org.kubek2k:springockito:${springockitoVersion}"
    testCompile "xmlunit:xmlunit:${xmlUnitVersion}"
    testCompile "org.awaitility:awaitility:${awaitilityVersion}"
    testCompile "com.jayway.restassured:rest-assured:${restassuredVersion}"
    testCompile "com.jayway.restassured:json-schema-validator:${restassuredVersion}"
    testCompile "org.antlr:ST4:${stAntlr4Version}"

    apt "com.google.auto.value:auto-value:${autovalueVersion}"
}
